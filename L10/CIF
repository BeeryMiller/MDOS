*-------------------------------------------------------------------------
*
*   FILE:   WDS1.NEW.CIF
*
*   NAME:   CONVERT INTEGER TO FLOATING
*           POINT NUMBER
*
*  VERSION: 5/25/87           BASE LINE
*
*  ON INPUT
*      R0 CONTAINS INTEGER, R1 CONTAINS POINTER
*  ON OUTPUT
*                           R1 POINTS TO FLOATING NUMBER
*-------------------------------------------------------------------------
CIF    MOV  @2(R13),R4        WILL CONVERT R4 POINTER
       MOV  R4,R6
       ANDI R6,>1FFF
       AI   R6,>A000
       SRL  R4,13
       MOVB @TSKMAP(R4),@MAPPER+5
       INC  R4
       ANDI R4,>0007
       MOVB @TSKMAP(R4),@MAPPER+6
       MOV  R6,R4
*
       LI   R7,100            FOR SPEED
       CLR  *R6+              AND CLEAR IT OUT
       CLR  *R6+
       CLR  *R6+
       CLR  *R6
       MOV  @4(R13),R0        get the number
       JEQ  CIF03             IF ZERO THEN DONE
*
       MOV  R0,R5             TEST FOR ZERO
       ABS  R0                ENSURE ITS POSITIVE
       LI   R3,>40            GET THE EXPONIENT
       C    R0,R7
       JL   CIF02
       CI   R0,10000
       JL   CIF01
       INC  R3
       MOV  R0,R1
       CLR  R0
       DIV  R7,R0
       SWPB R1
       MOVB R1,@3(R4)
CIF01  INC  R3
       MOV  R0,R1
       CLR  R0
       DIV  R7,R0
       SWPB R1
       MOVB R1,@2(R4)
CIF02  SWPB R0
       MOVB R0,@1(R4)
       SWPB R3
       MOVB R3,*R4
       INV  R5
       JLT  CIF03
       NEG  *R4
CIF03  RTWP
