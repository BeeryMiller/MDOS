
; 2.1.2021, Update for 9640 VDP addresses
;           ONLY use from level2 Vmsg routines

* VDP IO Ports
VDPRD     EQU >F100    >8800          * VDP Read Data
* VDPST     EQU >        >8802          * VDP Read Status
VDPWA     EQU >F102    >8C02          * VDP Write Address/Register
VDPWD     EQU >F100    >8C00          * VDP Write Data

* VDP Flag for setting Write address
VDWRITE     EQU     >4000          * ORI w/register holder target vdp write addr

*                                    ADDRESS
* Useful values if you have the WP in a register since VDP requires byte
* ADDRESSING
* these can be used with offset addressing, like @R9LB(R10) after
* PREPARING R10 WITH STWP R10
R0MB     EQU     >00
R0LB     EQU     >01
R1MB     EQU     >02
R1LB     EQU     >03
R2MB     EQU     >04
R2LB     EQU     >05
R3MB     EQU     >06
R3LB     EQU     >07
R4MB     EQU     >08
R4LB     EQU     >09
R5MB     EQU     >0A
R5LB     EQU     >0B
R6MB     EQU     >0C
R6LB     EQU     >0D
R7MB     EQU     >0E
R7LB     EQU     >0F
R8MB     EQU     >10
R8LB     EQU     >11
R9MB     EQU     >12
R9LB     EQU     >13
* R10 is where I store current WP
* R11 is return usually
* R12 is crubase usually
* R13 is rtwp
* R14 is ???
* R15 is VDPWA

* Macro - Set VDP read address
*       .defm setvdpra
*       SWPB    #1
*       MOVB    #1,@VDPWA
*       SWPB    #1
*       MOVB    #1,@VDPWA
*       .endm

* Macro - Set VDP write address
*       .defm setvdpwa
*       ORI     #1,VDWRITE
*       SWPB    #1
*       MOVB    #1,@VDPWA
*       SWPB    #1
*       MOVB    #1,@VDPWA
*       .endm


* BLWP routines for VDP operations like in EA

* VDP Write To Register
* R0 - MSB - Register to modify
* R0 - LSB - Value to write
VWTR
     DATA   >83E0          Workspace
     DATA   UVMTR

UVMTR
     MOV    @R0MB(R13),R0
     ORI    R0,>8000

*    .SETVDPRA R0
***** SETVDPRA R0 MACRO
     SWPB   R0
     MOVB   R0,@VDPWA
     SWPB   R0
     MOVB   R0,@VDPWA
*************************
     RTWP

* VDP MULTIPLE BYTE WRITE
* R0 - VDP RAM DESITINATION ADDRESS
* R1 - CPU RAM SOURCE ADDRESS
* R2 - NUMBER OF BYTES TO WRITE
VMBW
     DATA   >83E0          WORKSPACE
     DATA   UVMBW

UVMBW
     MOV    @R0MB(R13),R0
     MOV    @R1MB(R13),R1
     MOV    @R2MB(R13),R2

*    .SETVDPWA R0
**** SETVDPWA R0 ******
     ORI    R0,VDWRITE
     SWPB   R0
     MOVB   R0,@VDPWA
     SWPB   R0
     MOVB   R0,@VDPWA
***********************


HH   MOVB   *R1+,@VDPWD
     DEC    R2
     JNE    HH
     RTWP

* VDP MULTIPLE BYTE READ
* R0 - CPU DESTINATION
* R1 - VDP SOURCE ADDRESS
* R2 - NUMBER OF BYTES TO READ
VMBR
     DATA   >83E0          WORKSPACE
     DATA   UVMBR

UVMBR
     MOV    @R0MB(R13),R0
     MOV    @R1MB(R13),R1
     MOV    @R2MB(R13),R2

*    .SETVDPRA R1
***** SETVDPRA R1 MACRO
     SWPB   R1
     MOVB   R1,@VDPWA
     SWPB   R1
     MOVB   R1,@VDPWA
*************************

II   MOVB    @VDPRD,*R0+
     DEC     R2
     JNE     II
     RTWP


* VDP COPY
* R0 - VDP RAM DESITINATION ADDRESS
* R1 - CPU RAM SOURCE ADDRESS
* R2 - NUMBER OF BYTES TO WRITE
VDPCPY
     DATA     >83E0          WORKSPACE
     DATA     UVDPCPY

UVDPCPY
     MOV     @R0MB(R13),R0
     MOV     @R1MB(R13),R1
     MOV     @R2MB(R13),R2
JJ
*    .SETVDPRA R1
***** SETVDPRA R1 MACRO
     SWPB   R1
     MOVB   R1,@VDPWA
     SWPB   R1
     MOVB   R1,@VDPWA
*************************
     MOVB     @VDPRD,R4

*   .SETVDPWA R0
**** SETVDPWA R0 ******
     ORI    R0,VDWRITE
     SWPB   R0
     MOVB   R0,@VDPWA
     SWPB   R0
     MOVB   R0,@VDPWA
***********************

     MOVB   R4,@VDPWD
     INC    R0
     INC    R1
     DEC    R2
     JNE    JJ
     RTWP

