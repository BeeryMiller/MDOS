* FILENAME: VX07S
*
* ScrollWinDown
*
* R1x = Number of lines to scroll
* R2x = Row number of upper left corner
* R3x = Column number of upper left corner
* R4x = Row number of lower right corner
* R5x = Column number of lower right corner
* R6x = Character value for blank lines
* R7h = Foreground color for blank lines (NO COLOR IN TEXT MODES & M3)
* R7l = Background color for blank lines
*
X907LS  DATA    X907M0,X907M0,X907M2,X907M0
        DATA    X907M4,X907M4,X907M6,X907M6
        DATA    X907M6,X907M6
*
X907VC
X907L1  C       @4(R13),@8(R13)
        JH      X907RT
        C       @6(R13),@10(R13)
        JH      X907RT
        MOV     @2(R13),R10             * R10 increment
        MOV     @VIDMOD,R1
        SLA     R1,1
        MOV     @X907LS(R1),R1
        MOV     R11,@X9STAK
        BL      *R1
        MOV     @X9STAK,R11
X907RT  RT
*
X907M0 MOV  @8(R13),R9
       SETO @UPDOWN
       MOV  R10,@NROWS
       MOV  @10(R13),@NCOLS
       S    @6(R13),@NCOLS
       MOV  @6(R13),R0
       ANDI R0,7
       LI   R5,>FF
       SRC  R5,0
       MOV  R5,@LMSK0
       INV  R5
       MOV  R5,@LMSK1
       LI   R1,7
       MOV  @10(R13),R5
       ANDI R5,7
       S    R5,R1
       LI   R0,16
       S    R1,R0
       LI   R5,>FF
       SRC  R5,0
       MOV  R5,@RMSK0
       INV  R5
       MOV  R5,@RMSK1
       S    @4(R13),R9
       INC  R9
*
       C    R10,R9
       JH   X907K0
       MOV  R10,R10
       JNE  X907L0
X907K0 MOV  R9,R10
*
X907L0 MOV  @4(R13),R8        start row
       MPY  @D80,R8
       A    @6(R13),R5        r5 has address of char in screen buffer
       A    @SCRIMG,R5        r5 has vdp address of char in screen buffer
*
Y91020 C    R6,R8             done yet?
       JH   X907R0
*
       MOV  R6,R4
       A    @SCROFF,R4
       C    R4,@SCREND
       JL   Y91021
       S    @SCRADD,R4
Y91021 MOV  @10(R13),R2
       S    @6(R13),R2
*
       SWPB @SCRIMG+2
       MOVB @SCRIMG+2,@VDPWA
       SWPB @SCRIMG+2
       MOVB @SCRIMG+2,@VDPWA
*
       MOV  R5,R3
       ORI  R3,>4000
       SWPB R3
       MOVB R3,@VDPWA
       SWPB R3
       MOVB R3,@VDPWA
*
Y91022 MOVB *R4+,@VDPWD
       DEC  R2
       JOC  Y91022
       AI   R6,80
       A    @CHCOLS,R5
       JMP  Y91020
*
X907R0 RT
*
X907M2  RT
*
X907M4  MOV     @4(R13),R2
        MOV     R2,R8
        SLA     R8,8
        MOV     @6(R13),R6
        SLA     R6,3
        A       R6,R8                   * R8 top row
        MOV     R10,R7                  * R7 lines to blank
        MOV     @8(R13),R1
        MOV     R1,R4
        S       R2,R4
        INC     R4                      * R4 # of rows
        SLA     R1,8
        MOV     R1,R9
        A       R6,R9                   * R9 bottom row
        SLA     R10,8                   * R10 new increment
        S       R10,R9
        MOV     @10(R13),R5
        MOV     @12(R13),R2
        SRL     R6,3
        S       R6,R5
        INC     R5
        SLA     R5,3                    * R5 # of columns * 8
        C       R7,R4
        JHE     X907Z4
        MOV     R7,R7
        JNE     X907N4
X907Z4  MOV     R4,R7
        JMP     X907S4
X907N4  SWPB    @PTABLE+2
        MOVB    @PTABLE+2,@VDPWA
        SWPB    @PTABLE+2
        MOVB    @PTABLE+2,@VDPWA
        A       @PTABLE,R9
        SWPB    R9
        MOVB    R9,@VDPWA
        SWPB    R9
        ANDI    R9,>BFFF
        MOVB    R9,@VDPWA
        MOV     R5,R4
        LI      R1,LINBUF
X907O4  MOVB    @VDPRD,*R1+
        DEC     R4
        JNE     X907O4
        A       R10,R9
        SWPB    R9
        MOVB    R9,@VDPWA
        SWPB    R9
        ORI     R9,>4000
        MOVB    R9,@VDPWA
        MOV     R5,R4
        LI      R1,LINBUF
X907P4  MOVB    *R1+,@VDPWD
        DEC     R4
        JNE     X907P4
        S       R10,R9
        S       @PTABLE,R9
        SWPB    @CTABLE+2
        MOVB    @CTABLE+2,@VDPWA
        SWPB    @CTABLE+2
        MOVB    @CTABLE+2,@VDPWA
        A       @CTABLE,R9
        SWPB    R9
        MOVB    R9,@VDPWA
        SWPB    R9
        ANDI    R9,>BFFF
        MOVB    R9,@VDPWA
        MOV     R5,R4
        LI      R1,LINBUF
X907Q4  MOVB    @VDPRD,*R1+
        DEC     R4
        JNE     X907Q4
        A       R10,R9
        SWPB    R9
        MOVB    R9,@VDPWA
        SWPB    R9
        ORI     R9,>4000
        MOVB    R9,@VDPWA
        MOV     R5,R4
        LI      R1,LINBUF
X907R4  MOVB    *R1+,@VDPWD
        DEC     R4
        JNE     X907R4
        S       R10,R9
        S       @CTABLE,R9
        ANDI    R9,>BFFF
        C       R9,R8
        JEQ     X907S4
        AI      R9,-256
        JMP     X907N4
X907S4  ANDI    R2,>FF
        LI      R3,CHDEFT
        SLA     R2,3
        A       R2,R3
        ORI     R8,>4000
X907T4  SWPB    @PTABLE+2
        MOVB    @PTABLE+2,@VDPWA
        SWPB    @PTABLE+2
        MOVB    @PTABLE+2,@VDPWA
        A       @PTABLE,R8
        SWPB    R8
        MOVB    R8,@VDPWA
        SWPB    R8
        MOVB    R8,@VDPWA
        MOV     R5,R4
        SRL     R4,3
        S       @PTABLE,R8
X907U4  MOV     R3,R2
        LI      R6,8
X907V4  MOVB    *R2+,@VDPWD
        DEC     R6
        JNE     X907V4
        DEC     R4
        JNE     X907U4
        CLR     R1
        MOVB    @14(R13),R1
        MOVB    @15(R13),R2
        SLA     R1,4
        SOCB    R2,R1
        SWPB    @CTABLE+2
        MOVB    @CTABLE+2,@VDPWA
        SWPB    @CTABLE+2
        MOVB    @CTABLE+2,@VDPWA
        A       @CTABLE,R8
        SWPB    R8
        MOVB    R8,@VDPWA
        SWPB    R8
        MOVB    R8,@VDPWA
        MOV     R5,R4
        S       @CTABLE,R8
X907W4  MOVB    R1,@VDPWD
        DEC     R4
        JNE     X907W4
        AI      R8,256
        DEC     R7
        JNE     X907T4
        RT
*
X907M6  MOV     @8(R13),R1
        A       @PAGEPX,R1
        MOV     @6(R13),R0
        MOV     R0,R2
        MOV     R1,R3
        S       R10,R1                  * R10 increment, # of lines to blank
        MOV     R3,R9
        S       @4(R13),R9              * R9 # of rows
        S       @PAGEPX,R9
        INC     R9
        MOV     @10(R13),R4
        MOV     R1,R5
        S       R0,R4
        S       @4(R13),R5
        S       @PAGEPX,R5
        INC     R4
        INC     R5
        LI      R6,6
        MPY     R0,R6
        MOV     R7,R0
        A       @GRPHLF,R0
        SLA     R1,3
        AI      R1,7
        LI      R6,6
        MPY     R2,R6
        MOV     R7,R2
        A       @GRPHLF,R2
        SLA     R3,3
        AI      R3,7
        LI      R6,6
        MPY     R4,R6
        MOV     R7,R4
        SLA     R5,3
        LI      R6,>0800
        LI      R7,>90
        LI      R12,>80
        MOV     @VIDMOD,R8
        CI      R8,7
        JEQ     X907Y6
        LI      R7,>D0
        LI      R12,>C0
X907Y6  C       R10,R9
        JHE     X907Z6
        MOV     R10,R10
        JNE     X907N6
X907Z6  MOV     R9,R10
        JMP     X907O6
X907N6  BLWP    @VDCMD2
X907O6  MOV     R4,R2
        CI      R8,6
        JEQ     X907P6
        CI      R8,8
        JEQ     X907P6
        CI      R8,7
        JEQ     X907Q6
        MOV     @14(R13),R4
        ANDI    R4,>FF
        JMP     X907R6
X907P6  MOV     @14(R13),R5             *
        ANDI    R5,>FF                  *
        MOV     R5,R4                   * Calculate color for
        SLA     R5,4                    * blank lines
        SOC     R5,R4                   *
        JMP     X907R6                  *
X907Q6  MOV     @14(R13),R5
        ANDI    R5,>3
        MOV     R5,R4
        SLA     R5,2
        SOC     R5,R4
        MOV     R4,R5
        SLA     R5,4
        SOC     R5,R4
X907R6  MOV     @4(R13),R1
        A       @PAGEPX,R1
        SLA     R1,3
        SLA     R10,3
        MOV     R10,R3
        MOV     R12,R5
        BLWP    @VDCMD1
        RT

