*
* ROUTINE TO FREE UP USED SECTORS IN BIT MAP
* CALLERS R1   PTR TO FIRST SECTOR TO DELETE
* CALLERS R2   NUMBER OF CONTIGUOUS SECTORS TO DELETE base 0
*      USES R7,R4,r1
*
FRBITM DATA WRKREG,FRBITN
*
FRBITN MOV  @4(R13),R2        NUMBER OF SECTORS BASE 0
       INC  R2                NOW BASE 1
       MOV  @2(R13),R1        START DELETEING AT THIS SECTOR
*      MOV  R1,R0
       MOV  @16(R13),R8       BUFFER WE CAN USE
NEWSEC MOV  R1,R7             THIS CAN GO IN A SUBROUTINE JUST LIKE GETSEC
       SRL  R7,11             JUST LIKE GET SEC
       INC  R7                SECTOR 1 IS WHERE BIT MAP STARTS
       MOV  R7,@AUNUM
       MOV  R8,@RAMBUF        GET BUFFER
       BLWP @RSECDR
       JEQ  FRBIT1            ERROR
SAMSEC MOV  R1,R7
       SRL  R7,3
       ANDI R7,>00FE
       A    R8,R7
* R7 NOW POINTS TO THE WORD
* NOW POINT TO THE BIT
SAMWRD MOV  R1,R0
       LI   R4,>8000
       SRC  R4,0
       SZC  R4,*R7
* NOW TEST STOPPING CONDITION
       DEC  R2
       JEQ  FREED             JUMP IF COMPLETE
       INC  R1
       CZC  @H000F,R1         ELSE TEST IF ON SAME WORD
       JNE  SAMWRD
* NEXT WORD, SO TEST IF SAME SECTOR
       CZC  @H07F0,R1
       JNE  SAMSEC
* OTHERWISE GET NEXT SECTOR
       BLWP @WSECDR
       JNE  NEWSEC
FRBIT1 MOV  R0,*R13           BAD WRITE SECTOR
FRBIT2 RTWP
*
FREED  BLWP @WSECDR
       JEQ  FRBIT1            ERROR SECTOR WRITE
FRF002 CLR  *R13              NO ERROR
       JMP  FRBIT2
