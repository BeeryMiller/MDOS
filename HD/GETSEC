********************************************************************************
* ROUTINE TO GET A GOOD, UNUSED SECTOR. MARKS IT USED
* STARTING PTR IN R0  USES R6-R12 R1,R5
*     INPUT-R0 DESIRED STARTING SECTOR      (IN CALLERS WORKSPACE)
*           R1 BUFFER TO USE FOR I/O
*           R5 # OF SECTORS DESIRED (CONTIGUOUS)
*     OUTPUT-R0 ERROR CODE
*            R1 STARTING SECTOR
*            R5 NUMBER OF CONTIGUOUS SECTORS (BASE 1)
*  REGISTER USAGE
*      R0   SECTOR USED                      R7  POINTER WITHIN SECTOR
*      R1   BUFFER POINTER                   R8  POINTER TO WORD
*      R2   SECTOR CHANGE FLAG               R10 BIT POINTER WITHIN WORD
*      R4   SECTOR NUMBER VARIABLE
*      R5   # OF SECTORS TO GET              R12 # OF SECTORS ALREADY GOTTEN
*      R6   POINTER TO # OF SECTORS ON DISK
********************************************************************************
*
GETSEC DATA WRKREG,GETSSC
*
TOOHIG CLR  *R13         START WITH FIRST SECTOR
GETSSC MOV  *R13,R4
       CLR  R9                # OF SECTORS GOTTEN SO FAR
       MOV  @10(R13),R5
       BL   @SETAXB      SETS R1 AND RAMBUF POINTER TO AUX BUFFER
       MOV  @VOLUME,R6   TEST IF ON DISK
       SLA  R6,3
       C    R4,@CYLPLT-8(R6)
       JHE  TOOHIG       JUMP IF OFF DISK SEARCH
* R1 POINTS TO FREE DATA BUF
* START PTR IN R0 THEREFORE START LOOKING FROM THAT POINT 256*8=2**11 SECR
* SECTOR BITS PER SECTOR
*
NSEC   MOV  R4,R7        GET SECTOR TO START LOOKING
       SRL  R7,11
       INC  R7           ADD OFFSET TO START
       MOV  R7,@AUNUM    STORE IT AND GET THAT SECTOR
       CLR  R2           CLEAR SECTOR CHANGE FLAG
       BLWP @RSECDR
       JEQ  NSEC7        I/O ERROR
*
* SECTOR OF SECTORS USED POINTERS IN BUFFER AT R1
* NOW LOOK FOR FREE SECTORS
*
       MOV  R4,R7        GET POINTER TO WORD
       SRL  R7,3
       ANDI R7,>00FE     R7 NOW POINTS TO WORD BASE 256
NXTWRD MOV  R4,R0
       MOV  R1,R8        GET POINTER OFFSET TO ACTUAL BUFFER
       A    R7,R8
* IF NOT TOTALLY USED THEN START SEARCHING AT SECTOR DESIGNATED
H8000  EQU  $+2
CBH80  EQU  $+2
       LI   R10,>8000
       C    *R8,@HFFFF   TEST IF ENTIRELY USED
       JNE  NSEC9
       MOV  R9,R9        ANY GOTTEN SO FAR?
       JNE  NSEC1        YES, SO CAN NOT CONTINUE
       JMP  TOTUSE
*
NSEC9  SRC  R10,0        START SEARCH BY VALUE IN R0
NSEC6  CZC  *R8,R10      TEST IF BIT IS ZERO
       JNE  NOTZER       JUMP IF NOT ZERO
       MOV  R9,R9        TEST IF THE FIRST TIME THROUGH
       JNE  NSEC2        NO, HAVE SET THE STARTING SECTOR ALREADY
NSEC4  MOV  R4,@2(R13)   SAVE STARTING SECTOR
NSEC2  SOC  R10,*R8      IF ZERO, MAKE IT ONE
       SETO R2           CHANGE SECTOR
* HAVE WE GOTTEN ENOUGH SECTORS?
       INC  R9
       DEC  R5
       JEQ  NSEC1
* NOT YET, SO TRY NEXT CONTIGUOUS SECTOR
       SRL  R10,1         NEXT SECTOR
       JNE  NSEC6        GO TRY THIS WORD
*
TOTUSE ANDI R4,>FFF0     GO TO NEXT WORD
       AI   R4,>0010     BY ADDING ONE
       C    R4,@CYLPLT-8(R6) TEST FOR END OF DISK
       JHE  TOOLAR
       INCT R7           ADD 2 TO 256 COUNTER
       MOVB R7,R7
       JEQ  NXTWRD
       MOV  R9,R9        HAVE WE GOTTEN ANY BYTES YET?
       JEQ  NSEC         NO
       CLR  R2
       BLWP @WSECDR      YES, SO SAVE OUT THE SECTORS USED
       JEQ  NSEC7
       JMP  NSEC
*
NOTZER MOV  R9,R9        ANY SECTORS SO FAR?
       JNE  NSEC1
       ANDI R4,>FFF0     CAN'T BE THIS ONE, SO SINCE ONE IS AVAILABLE,
* TAKE IT (MAY WANT NEXT CONTIGUOUS ONE SOMETIME IN FUTURE
       LI   R10,>8000    NOW GET THE FREE SECTOR IN WORD
NSEC5  CZC  *R8,R10
       JEQ  NSEC4        JUMP IF ZERO FOUND
       SRC  R10,1
       INC  R4
       JMP  NSEC5        CONTINUE UNTIL FOUND
*
NSEC1  CLR  R0
       JMP  NSEC11
*
TOOLAR MOV  R9,R9        ANY GOTTEN SO FAR?
       JNE  NSEC1        YES, SO GOOD RETURN
       MOV  *R13,R4      NO, SO DID IT START FROM 0?
       JNE  TOOHIG       NO, SO TRY FROM THERE
       LI   R0,>8000     OUT OF ROOM
NSEC11 MOV  R0,*R13
       MOV  R2,R2        SAVE THIS SECTOR?
       JEQ  NSEC10       NO
       BLWP @WSECDR      WRITE IT BACK
       JNE  NSEC10       NO ERROR
*
NSEC7  MOV  R0,*R13      RETURN ERROR CODE
NSEC10 MOV  R9,@10(R13)  RETURN # OF SECTORS GOTTEN
       RTWP              ALL DONE
