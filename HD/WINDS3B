; WINDS3B-S
; Split 11-18-2023

BIO
*
*      BREAD        pab in         read sectors from any disk file
*                        CAN BE USED WITH MAIN DIRECTORY OF HARD DISK
*      byte  0      0A
*            1      -
*            2      -
*            3      \
*            4       \
*            5        \ buffer address
*            6      \
*            7       \ sector offset within file to begin read
*            8      -
*            9      -
*           10      transfer flag, 0 is cpu, non-zero is VDP
*           11      0
*           12      \  number of sectors to read from file
*           13       \ if zero, transfers files ID info into buffer
*           14      MSB OF SECTOR OFFSET
*           15      name length <byte count>
*           16+     text of name
*
*      BREAD        pab out
*
*      byte  0      -
*            1      -
*            2      error code
*            3      \ updated to point to location in memory after the
*            4       \ last byte successfully read from disk
*            5        \
*            6      \  (sector offset of last sector read)+1
*            7       \ if read error, points to the bad sector
*            8      -
*            9      -
*           10      -
*           11      0
*           12      \  count of number of sectors
*           13       \ not read due to error condition
*           14      MSB OF SECTOR OFFSET
*           15      name length <byte count>
*           16+     text of name
*
* @buffer is file ID info as follows:
*
*      byte:
*=           0,1     extended record length, used for files with records
*=                   longer than 255 bytes
*=           2       file status flags
*=
*= BIT:  Msb 7 6 5 4 3 2 1 0   MEANING
*=           | | | | | | | +-> 0-DATA 1-PROGRAM
*=           | | | | | | +---> 0-ASCII 1-BINARY
*=           | | | | | +-----> 2              RESERVED
*=           | | | | +-------> 3              0-NOT PROTECTED 1-PROTECTED
*=           | +-+-+---------> 4-6            RESERVED
*=           +---------------> 7              0-FIXED RECORD 1-VARIABLE LENGTH
*=
*=           3       number of records which can fit in a sector (256 bytes)
*=                   0 for program image, extended record lengths
*=           4,5     number of sectors reserved for file
*=           6       number of bytes used in last sector of file
*=                   (0 means all 256 are used)
*=           7       logical record length
*=                   0 for program image, extended record lengths
*=           8,9     (bytes reversed)
*=                   for fixed files, (highest used record number)+1
*=                   for program image, variable files, number of sectors
*=                   actually used
*=           10,11   date of creation     bits: yyyy yyyM MMMd dddd
*=           12,13   time of creation           hhhh hmmm mmms ssss
*=                                              seconds are / 2, with remainder
*=                                              discarded
*=           14,15   date of last change   "
*=           16,17   time of last change   "
*=           18      MSB LEVEL 2
*=           19      MSB LEVEL 3
*
*
*
*      BWRITE       pab in         write sectors to any disk file
*                      CAN BE USED WITH MAIN DIRECTORY OF HARD DISK
*      byte  0      0B
*            1      -
*            2      -
*            3      \
*            4       \
*            5        \ buffer address
*            6      \
*            7       \ sector offset within file to begin write
*            8      -
*            9      -
*           10      transfer flag, 0 is cpu, non-zero is VDP
*           11      0
*           12      \  number of sectors to write to file
*           13       \ if zero, creates file with ID info from buffer
*                     \ buffer info described in BREAD
*           14      MSB OF SECTOR OFFSET
*           15      name length <byte count>
*           16+     text of name
*
*      BWRITE       pab out
*
*      byte  0      -
*            1      -
*            2      error code
*            3      \  points to location in memory after last byte
*            4       \ successfully written to disk
*            5        \
*            6      \  (sector offset of last sector written)+1
*            7       \ if write error, points to the bad sector
*            8      -
*            9      -
*           10      -
*           11      0
*           12      \  count of number of sectors not
*           13       \ written due to error condition
*           14      MSB OF SECTOR OFFSET
*           15      name length <byte count>
*           16+     text of name
*
       LI   R0,>6000          BINARY I/O ONLY ON MAIN DIR
       ABS  @ANDIR            IS IT 0?
       JNE  BAD110            NO, CAN'T DO IT
*
       MOV  @CHRCNT,R1        sector count
       JEQ  BIO008            done already...
* DIRECT SECTOR I/O ROUTINE
* TEST IF DISK IS FORMATTED WITH PARTITION COMMAND
* THEN TEST IF IN DSK1 EMULATION MODE, THEN CONTINUE OPERATION
       SETO @DSECIO           RETURN "RAW" SECTOR ERROR CODES
       MOV  @VOLUME,R2
       SLA  R2,3
       AI   R2,CYLPLT-8       POINT TO THE DRIVES PARMS
BIO005 MOV  @RECNUM,R5        MID AND LSB OF SECTOR NUMBER
       MOVB @SCNOFF,R4        GET SECTOR OFFSET MSB
       SRL  R4,8
       MOV  @6(R2),R11        GET THE NUMBER OF SECTORS PER AU
       SRL  R11,12
       INC  R11
       DIV  R11,R4            NOW AU # IN R0 AND SECTOR IN AU IN R1
       JNO  BIO007            NO OVERFLOW
       LI   R0,>8000          OVERFLOW CODE
BIO010 JMP  BAD110
*
BIO007 MOV  R4,@AUNUM         SET UP FOR SECTOR I/O
       MOV  R5,@SCINAU
       MOV  R8,@RAMBUF        BUFFER TO USE
       MOV  @BUFADR,@PASADL   ADDRESS IN RAM FOR DATA
       MOVB @BUFADH,@PASADH
       MOV  R8,@PASDSR        ADDRESS IN DSR SPACE
       MOV  @H0100,@PASCNT    256 BYTES
       MOVB @CPUFLG,@PASVDP
* NOW THE NUMBER OF SECTORS TO TRANSFER IS IN R1
       CB   @OPCODE,@BREDOP   IS IT BINARY READ?
       JEQ  BIO003            YES
       SETO @PASRW            READ THE DATA FIRST
       BLWP @PASDAT           IT IS WRITE, SO WRITE THE DATA TO THE BUFFER
       BLWP @WSECTR
       JEQ  BIO010
       JMP  BIO002
*
BIO003 BLWP @RSECTR
       JEQ  BIO010
       CLR  @PASRW            WRITE THE DATA TO THE TASK
       BLWP @PASDAT
BIO002 INC  @RECNUM           POINT TO NEXT SECTOR
       JNC  BIO004
       AB   @CBH01,@SCNOFF
BIO004 A    @H0100,@BUFADR    UP THE TASK DATA PTR
       JNC  BIO006
       AB   @CBH01,@BUFADH
BIO006 DEC  @CHRCNT
       JNE  BIO005
BIO008 B    @SYSRTN           ALL DONE
       PAGE
BIO000 MOV  @CHRCNT,R0        IS IT GET PARMS, OR CREATE PARMS?
       JNE  BIO100            NO, JUST SIMPLE DATA SECTOR I/O
       CLR  R0                flag to say that file existed already
       JMP  BIO050
*
BINWRP SETO R0                flag to say that FCB is already done
*
BIO050 MOV  @H0014,@PASCNT    20 BYTES TO TRANSFER
       MOV  @BUFADR,@PASADL   ADDRESS TO SEND DATA
       MOVB @BUFADH,@PASADH
       MOVB @CPUFLG,@PASVDP
       MOV  R9,R10
       AI   R10,XRECLN
       MOV  R10,@PASDSR       START TRANSFER AT EXTENDED RECORD LENGTH
       CB   R6,@BREDOP        IS IT GET PARMS?
       JEQ  FB0000            YES, GET THE PARMS
       SETO @PASRW            FROM TASK TO DSR
       MOV  R0,R0             new FCB already in place?
       JNE  BIO055            yes, no delete necessary now
*
* don't do a delete...to allow append using BWRITE!
*
       B    @ATTOP            FIRST GO TO DELETE THE EXISTING FILE  @@ no!
*
BIO055 CLR  *R3               WRITE BINARY PARAMETERS
       BLWP @PASDAT           GET THE PARMS
       MOV  @FI(R9),R4        GET THE MSB OF THE # OF SECTORS
       SWPB R4
       MOVB R4,@NIBLES(R9)    SAVE # ALLOCATED IF VARIABLE
       SZCB R4,R4             NOW HAVE THE MSB OF # OF SECTORS
       MOV  @LEV2RC(R9),R5    NOW THE MID AND LSB OF # OF SECTORS
       CLR  @LEV2RC(R9)       ZERO OUT THE INITIAL ALLOCATION
       CLR  R7                USE R7 FOR ANY ERROR CODE
* now adjust FOR BASE 0 AU'S
       DEC  R5                THIS ROUTINE ALSO TESTS FOR 0 ALLOCATION
       JOC  BINW00            OKAY, NO ALLOCATION
       DEC  R4
       JNC  BINW01
BINW00 BLWP @GETAUS
       MOV  R0,R7
BINW01 SOCB @CBH10,@FSTATF(R9)               file has changed
*
       INC  R5                yalfu
       JNC  BINW03
       INC  R4
BINW03 SLA  R4,12
       SOCB R4,@NIBLES(R9)
       MOV  R5,@LEV2RC(R9)
*
       BL   @WRTFCB
       MOV  R0,R0             ERROR?
       JNE  BINW02
       MOV  R7,R0             ERROR IF ANY
BINW02 B    @BADOP
*
FB0000 CLR  @PASRW            PASS THE FILE PARMS BACK TO THE CALLER
       MOVB @NIBLES(R9),R10   NOW GET THE EXTENDED LENGTH LVL2/USED
       MOVB R10,R11           FIX IT UP    NEED TO RETURN LEVEL2/USED WORD
       ANDI R10,>0F00
       SWPB R10
       SRL  R11,4
       MOVB R11,R10
       MOV  R10,@FI(R9)
       BLWP @PASDAT           READ IT ALL
       MOV  @CHRFI,@FI(R9)    REPLACE FI
       B    @SYSRTN           ALL DONE
*
BIO100 MOV  @LEV2RC(R9),@BINMX1
       MOV  @NIBLES(R9),R4
       SRL  R4,12
       MOV  R4,@BINMX2
*
BIO101
       MOV  @RECNUM,R5        SET UP FOR SECTOR I/O
       MOVB @SCNOFF,R4
       SRL  R4,8
*
       C    R4,@BINMX2
       JL   BIO110
       JH   BIOEOF
       C    R5,@BINMX1
       JL   BIO110
*
BIOEOF LI   R0,>A000          eof error
       JMP  BINW02
*
BINMX1 DATA 0
BINMX2 DATA 0
*
BIO110 MOV  @BUFADR,@PASADL   ADDRESS IN RAM FOR DATA
       MOVB @BUFADH,@PASADH
       MOV  R8,@PASDSR        ADDRESS IN DSR SPACE
       MOV  @H0100,@PASCNT    256 BYTES
       MOVB @CPUFLG,@PASVDP
       BLWP @GETAUS           GET THE SECTOR SET UP
       MOV  R0,R0
       JNE  BINW02
*
       MOV  R8,@RAMBUF        BUFFER TO USE  yalfu
*
       CB   @OPCODE,@BREDOP   IS IT BINARY READ?
       JEQ  BIO103            YES
       SETO @PASRW            READ THE DATA FIRST
       BLWP @PASDAT           IT IS WRITE, SO WRITE THE DATA TO THE BUFFER
       BLWP @WSECTR
       JEQ  BINW02
       JMP  BIO102
*
BIO103 BLWP @RSECTR
       JEQ  BINW02
       CLR  @PASRW            WRITE THE DATA TO THE TASK
       BLWP @PASDAT
BIO102 INC  @RECNUM           POINT TO NEXT SECTOR
       JNC  BIO104
       AB   @CBH01,@SCNOFF
BIO104 A    @H0100,@BUFADR    UP THE TASK DATA PTR
       JNC  BIO106
       AB   @CBH01,@BUFADH
BIO106 DEC  @CHRCNT
       JNE  BIO101
       B    @SYSRTN           ALL DONE

       PAGE
********************************************************************************
*
*      RENAME FILE, SO TEST IF THE NEW NAME EXISTS
*      IF NOT THEN PUT NEW NAME IN ALL FDR'S AND ADJUST POINTER
*        IN INDEX OF POINTERS
*         POINTERS ARE IN THE R8 BUF AND THE R9 BUF CONTAINS THE TOP FDR
********************************************************************************
FREN   MOV  @AUNUM,R7         SAVE THE LOCATION OF TOP FDR FOR LATER USE
       BL   @PBTOCB           MOVE THE NEW FILE NAME TO THE COMBUFFER
       MOV  @NOPTRS,R11
       SRL  R11,8             GET NUMBER OF ENTRIES TO LOOK AT
       BLWP @FINDFL           LOOK FOR IT AND HOPE WE DON'T FIND IT
       JEQ  FNEXT             FOUND IT SO ERROR 7
       MOV  R0,R0
       JNE  BAD211            HAREWARE ERROR
       MOV  R8,R11            FIRST PUT POINTER IN CORRECT ORDER
       AI   R11,256
       MOV  R7,R1             LOCATION ON DISK
       MOV  R2,R4
FRNAM1 MOV  *R2,R0            PUT THE NEW POINTER IN THE LIST
       MOV  R1,*R2+
       MOV  R0,R1
       C    R2,R11            ARE WE AT THE END?
       JL   FRNAM1
       MOV  R8,R5
       INCT R4
FRNAM2 C    *R5+,R7            NOW MOVE OUT THE PREVIOUS POINTER
       JNE  FRNAM2
       C    R5,R4
       JEQ  FRNAM2
       MOV  R5,R10
       DECT R10
FRNAM3 MOV  *R5+,*R10+
       C    R5,R11
       JL   FRNAM3
       MOV  R1,@-2(R11)
       MOV  R8,@RAMBUF
       MOV  @INDPTR(R3),@AUNUM
       BLWP @WSECTR           WRITE THE POINTERS BACK
       JEQ  BAD211
       MOV  R9,@RAMBUF
       MOV  R7,@AUNUM
       BLWP @RSECTR           READ THE FDR
       JEQ  BAD211
FRNAM4 LI   R0,COMBUF         COMBUF TO R9 BUFFER
       MOV  R9,R11
       MOV  *R0+,*R11+
       MOV  *R0+,*R11+
       MOV  *R0+,*R11+
       MOV  *R0+,*R11+
       MOV  *R0,*R11
       BLWP @WSECTR
       JEQ  BAD211
       BL   @GETCLD
       JEQ  BAD211            DONE OR ERROR
       JMP  FRNAM4
*
FNEXT  LI   R0,>E000          FILE EXISTANCE PROBLEM
       JMP  BAD211
       PAGE
FFND
* TO BE HERE, FILE WAS FOUND DURING SEARCH
* THEREFORE CAN DO ALL FILE OPERATIONS EXCEPT,
* IF FILE IS PROTECTED THEN CAN'T SAVE OR IF OPEN, THEN CAN ONLY INPUT
* WE KNOW THE FOLLOWING
*  r8 POINTS TO SECTOR BUFFER OF FILE POINTERS
*  r9 POINTS TO FCB AS IT EXISTS NOW
*  R3 POINTS TO FILE NAME BUFFER
* SAVE TOP FCB IN FILE NAME PTR
       MOV  @AUNUM,@FNLOCF(R3)
*  OPCODE AND FLAGS in r6 already
       CLR  R2
       MOVB @FSTATF(R9),R2    GET FILE STATUS FLAGS
* FIRST CHECK IF OPCODE STATUS
       CB   R6,@CBH09
       JNE  NSTAT             BRANCH IF NOT STATUS
* TO BE HERE OPCODE IS STATUS OF NOT OPEN FILE
* SO TEST ALL STATIC TESTS IN ROUTINE GETSTT
GETSTT
* TO BE HERE, FIND THE STATIC PARAMTERS OF A FILE
* FILE STATUS FLAGS msb R2
* RETURNS BYTE IN R0 MSBYTE
* USES R0,R2
* BITS ARE MSB     1        2        3          4         5         6        LSB
* FDR 0  fixed           NORMAL  NO UPDATE  not prot             Display Dat fil
*     1  variab          "DSK1"   UPDATED   protect              Intern  Pgm fil
*
* STT 0 F EXIST NOT PRO  future   DISPLAY   DATA FILE   FIXED    NOT EOM  NOT EF
*     1 NOWHERE PROTECT           INTERNAL  PROG FILE   VARIABLE EO MEDIA  EOF
*
       MOV  R2,R1
       SLA  R2,3
       SRL  R1,5
       SOCB R1,R2
       ANDI R2,>5C00
       MOVB R2,@SCNOFF        PLACE STATUS FLAGS IN SCREEN OFFSET
LOADED B    @SYSRTN           RETURN TO SYSTEM
       PAGE
FBWR
FBRD   B    @BIO000
*
NSTAT  CB   R6,@CBH0A         IS IT BINARY READ?
       JL   NSTAT9            NO, NORMAL DSR CALL
       JEQ  FBRD              YES, IT IS BREAD
       CB   R6,@CBH0C         IS IT PROTECT?
       JL   FBWR              IT IS BWRITE
       JH   FREN              IT IS RENAME
*  TO BE HERE, IT IS PROTECT FILE, SO SET THE FDR CORRECTLY
       LI   R2,>0800          PROTECT BIT
       CLR  R1
       MOVB @FLGSTS,R0        IF 0 THEN UNPROTECT
       JEQ  FPROT0            UNPROTECT
       MOVB R2,R1
FPROT0 SZCB R2,@FSTATF(R9)    ZERO OUT THE BIT
       SOCB R1,@FSTATF(R9)    SET THE BIT IF SUPPOSED TO
       BLWP @WSECTR
       JEQ  BAD211            WRITE ERROR
       BL   @GETCLD
       JEQ  BAD211            ALL DONE, NO CHILD FDR AND 0 IN R0 OR ERROR IN R0
       JMP  FPROT0
*
NSTAT9 COC  @H0800,R2         TEST IF FILE IS PROTECTED
       JNE  NOTPRO            BRANCH IF NOT PROTECTED
* FILE IS PROTECTED SO TEST IF SAVE
       CB   R6,@CBH06
       JNE  NPRO1             ERROR BRANCH IF TRYING TO SAVE OVER PROT FILE
BADOP1 LI   R0,>2000          ERROR TRYING TO MODIFY PROTECTED FILE
BAD211 B    @BADOP
*
* TEST IF DELETE
NPRO1  CB   R6,@CBH07
       JEQ  BADOP1            ERROR BRANCH TRYINNG TO DELETE PROT FILLE
* NOW TEST IF OPEN
       MOVB R6,R6
       JNE  NOTPRO            BRANCH IF NOT OPEN
* NOW TEST IF INPUT MODE
       COC  @H0004,R6
       JNE  BADOP1            JUMP TRYING TO UPDATE PROTECTED FILE
       CZC  @H0002,R6
       JNE  BADOP1            JUMP IF TRYING TO UPDATE PROTECTED FILE
NOTPRO
* TO BE HERE, ALL FILE OPERATIONS ARE FINE
       CB   R6,@CBH07      TEST IF DELETE
       JNE  NDEL         BRANNCH IF NNOT DELETE
       B    @DELOOP
*
* NOW TEST IF SAVE
NDEL   CB   R6,@CBH06      TEST IF SAVE
       JNE  NOPNOT       IF SAVE THEN DELETE FILE FIRST
DSAVE
* TO BE HERE, FREE UP ALL SECTORS POINTED TO BY FCB EXCEPT FCB ITSELF
* WE KNOW THE FOLLOWING
* PARENT POINTS TO WORK DATA BUFFER
* SIBLIN POINTS RO TOP LEVEL FCB IN RAM
* R3 POINTS TO FILE NAME POINTER BLOCK
* THEREFORE, NEED TO FREE ALL SECTORS OF DATA + FCB'S GOTO ATTOP
       B    @ATTOP
*
NOPNOT
* NOW TEST FOR OPCODE LOAD
* WE KNOW THE FOLLOWING
* FCB IN SIBLIN, DATA BUF INN PARENT R3 HAS FILENNAME BUF
       CB   @CBH05,R6      TEST IF LOAD
       JEQ  LOAD1        BRANCH IF NNOT LOAD
       B    @NLOAD            BRANCH TO OPEN
*
* FIRST TEST IF PROG FILE
LOAD1  LI   R0,>4000          ERROR CODE FOR NON PROG FILE
       COC  @H0100,R2         TEST IF PROG FILE
       JNE  BAD211            ERROR TRYING TO LOAD A NNONE PROG FILE
* NOW TEST FOR SUFFICIENT AREA (GET LSW OF LENGTH IN R2)
       MOV  @LEV2RC(R9),R2    NUMBER OF SECTORS IN LENGTH
       MOVB @EOFOST(R9),R11   IF SECTOR OFFSET IS ZERO
       JEQ  L2A                 THEN CORRECT # OF SECTORS
       DEC  R2                   ELSE ADJUST DOWNWARD
L2A    LI   R0,>8000          ERROR CODE FOR NOT ENOUGH MEM
       MOVB @CHRCNH,R4        SAVE MEMORY SPACE PARMS
       MOV  @CHRCNT,R5
*
       CLR  @RECNUM           return true program image length
       MOV  R2,R6
       SRL  R6,8
       MOV  R6,@RECNUM
       MOV  R2,R6
       MOVB R11,R6
       SWPB R6
       MOV  R6,@LRECLN        RETURN LSW
*
       CB   R2,R4           TEST MSB
       JH   BAD211            FILE TOO LARGE FOR MEMORY
       JL   L4C               SMALL ENOUGH TO FIT
       C    R6,R5           MSB'S EQUAL SO TEST LSW
       JH   BAD211
* TEST IF LENGTH IS ZERO AND IF SO, THEN DONE
L4C    SRL  R11,8
       CLR  R5                STARTING SECTOR IS ALWAYS 0
       CLR  R4
L4E    MOV  R2,R2             TEST FULL SECTORS LEFT
       JNE  L4B               YES, IT IS NON ZERO
       MOV  R11,R11           TEST OFFSET FOR 0
       JEQ  LOADED            ALL DONE
* NOW LOAD PROGRAM, WE KNOW THE FOLLOWING
*      R2 CONTAINS THE NUMBER OF FULL SECTORS
*      R11 MSB CONTAINS THE NUMBER OF BYTES IN ANY LAST SECTOR
*               IF NON 0
L4B    MOV  R8,R7             START OF SECTOR
       BLWP @GETAUS
       MOV  R0,R0             DEVICE ERROR
       JNE  BAD211
       MOV  R8,@RAMBUF
       BLWP @RSECTR           GET DATA SECTOR
       JEQ  BAD211            READ SECTOR ERROR
* NOW CALCULATE THE NUMBER OF BYTES TO USE OF THIS SECTOR
       LI   R6,256
       MOV  R2,R2             IS THIS THE LAST SECTOR?
       JNE  L4A               YES, SO IT IS A PARTIAL
       MOV  R11,R6            LAST SECTOR
       CLR  R11
       INC  R2                JUST TO MAKE IT EQUAL
L4A    DEC  R2
* NOW LOAD BYTE BY BYTE IN MEMORY
       MOVB @CPUFLG,@PASVDP   VDP/CPU
       CLR  @PASRW            DSR TO USER
       MOV  @MYUADL,@PASADL   USER ADDRESS
       MOVB @MYUADH+1,@PASADH
       MOV  R6,@PASCNT       NUMBER OF BYTES
       MOV  R8,@PASDSR        WHERE IT IS IN CPU RAM
       BLWP @PASDAT
       BLWP @ADDADR
* TO BE HERE SECTOR COMPLETE SO GET NEXT SECTOR OF DATA
L6     INC  R5                TEST IF THIS CONTIGUOUS SECTORS COMPLETE
       JMP  L4E
       PAGE
NTSAVE
* TO BE HERE WE HAVE AN OPEN OUTPUT OR UPDATE TO A NONEXIST FILE
* WE KNOW IN THE FILE NAME BLOCK, THERE IS
* 1 A PTR TO THE FCB                         2 DATA BUFFER NOT IN RAM=0
* 3 LOGICAL OFFSET LEVEL  2=0                4 PHYSICAL LOC OF FCB ON DISK
* 5 PHYSICAL LOC OF DATA BUF ON DISK=0       6 OFFSET =0, DRIVE # LOADED
* 7 PATHANME IS LOADED 40 CHAR
*
* WE KNOW IN THE FCB BLOCK
* 1 FILE NAME (10 CHAR)                      2 CHARACTERS "FI"
* ALL OTHER BYTES ARE ZERO
* NOW GET FLAGS TO STORE IN FILE FCB
       BL   @PBFST
*
* NOW LOAD TIME OF CREATION AND LAST UPDATE (NOW ONE AND THE SAME)
*
FNLR   SOCB @CBH10,R5         file has changed since last archive
       MOVB R5,@FSTATF(R9)    STORE FLAGS IN FCB
*
* NOW TEST LOGICAL RECORD LENGTH
*
RLEN1  MOV  @LRECLN,R5           TEST AND STORE LOG REC LENGTH
       JNE  RLEN2             BRANCH IF NOT ZERO
       LI   R5,80             SET DEFAULT TO 80 IN PAB
*
* FILE NAME BUFFER HAS PHYSICAL LOCATION OF FCB ON DISK
* NOW GET INITIAL ALLOCATION
*
RLEN2  MOV  R5,@LRECLN        yalfu
       MOVB R5,R5             IS IT GREATER THAN 255?
       JNE  RLEN5             YES, SO USE EXTENDED RECORD LENGTH
       COC  @H0010,R6         IS IT VARIABLE LENGTH
       JNE  RLEN2A            NO
       CI   R5,255            IS IT 255?
       JHE  RLEN5             YES, SO USE EXTENDED LENGTH
RLEN2A SWPB R5
       MOVB R5,@RECLEN(R9)    STORE RECORD LENGTH
       SWPB R5
       COC  @H0010,R6         FIXED?
       JNE  RLEN2C            IT IS FIXED
       INC  R5
RLEN2C CLR  R10               GET NUMBER OF REC PER AU
       LI   R11,256
       DIV  R5,R10            R10 HAS # OF REC/AU
       SWPB R10
       MOVB R10,@RECPAU(R9)
       SWPB R10
       MOV  @RECNUM,R5
       JEQ  RLEN3
       CLR  R4
       DIV  R10,R4
       MOV  R5,R5             IS THERE A REMAINDER?
       JNE  RLEN2B            YES
       DEC  R4                ELSE MAKE BASE 0 FOR # OF SECTORS
RLEN2B MOV  R4,R5
       CLR  R4
RLEN5B BLWP @GETAUS           GET PHYSICAL AU'S
       MOV  R0,R0             ERROR?
       JNE  BAD012            ERROR, I/O OR OVERFLOW ERROR
RLEN3  BL   @WRTFCB           WRITE FCB FOR FUTURE REF
       JEQ  BAD012            ERROR IN I/O
RLENPC CLR  @RECNUM           ZERO OUT #OF RECORDS FOR FUTURE
       B    @FINISH
*
RLEN5  MOVB @CBH00,@RECLEN(R9)  IT IS A EXTENDED RECORD LENGTH TYPE FILE
       MOV  R5,@XRECLN(R9)
*  NOW FOR INITIAL ALLOCATION PROBLEM
*   NEED TO ADD 2 TO VARIABLE RECORD FILES
       COC  @H0010,R6         TEST IF VARIABLE
       JNE  RLEN5A            IT IS FIXED
       LI   R0,>4000
       INCT R5
       JOC  BAD012            RECORD LENGTH TOO LARGE
RLEN5A MOV  @RECNUM,R11
       JEQ  RLEN3             NO INITIAL ALLOCATION
       MOV  R5,R4
       MPY  R11,R4            # OF BYTES IN R4,R5
       SWPB R5                GET SECTORS, NOT BYTES
       MOVB R5,R11            SAVE ROUND OFF
       SWPB R4
       MOVB R4,R5
       SZCB R4,R4
       MOVB R11,R11           IS THERE A REMAINDER?
       JNE  RLEN5B            YES, SO CORRECT
       DEC  R5
       JNC  RLEN5B
       DEC  R4
       JMP  RLEN5B
       PAGE
NLOAD
* TO BE HERE MUST BE OPEN
* IF OPEN OUTPUT THEN USE THE NEW PARAMETERS AND ZERO RECORD LENGTHS
* TEST FOR OUTPUT BUT FIRST GET FLAGS
       BL   @PBFST            GET FILE STATUS FLAGS FROM PAB
       SETO *R3             RESERVE FILE BLOCKS
       MOVB @DRIVID,@1(R3)
* R6 HAS RAW OPEN STATUS FLAGS
       COC  @H0002,R6
       JNE  RLENV
       CZC  @H0004,R6
       JNE  RLENV
* TO BE HERE THEN OPEN OUTPUT SO GO TO DELETE IT FIRST IF FLOPPY
       CLR  @FSTATF(R9)       ZERO FLAGS AND REC/AU
       CLR  @EOFOST(R9)       ZERO EOF AND REC LEN
       CLR  @LEV3RC(R9)       ZERO HIGHEST FIXED WRITTEN OR VAR USED
       SZCB @CBH0F,@NIBLES(R9)
       JMP  NTSAVE       GO TO GET FLAGS AND REC LENNGTH
*
*  TO BE HERE, MUST BE OPEN NOT OUTPUT SO SEE IF FILE PARMS MATCH
*  SIBLIN POINTS TO FILE BUFFER
*
RLENV  MOVB @FSTATF(R9),R7    GET FILE STATUS FLAGS
       LI   R0,>4000          ERROR CODE TRYING TO OPEN PROG FILE FOR READ
       ANDI R7,>8300          TEST IF PROGRAM FILE
*  NOW TEST IF FILE TYPES COMPATIBLE
* R5 HAS PAB STATUS IN FCB FORM R5 HAS STATUS IN PAB FORM
       CB   R5,R7             TEST IF COMPATIBLE
       JEQ  COMPTB
BAD012 B    @BADOPC           CLEAR OUT THE PAB IN USE AND ERROR OUT
*
*  NOW IF FLAGS ARE COMPATIBLE,TEST RECORD LENGTH
COMPTB MOVB @RECLEN(R9),R7    GET RECORD LENGTH OF FILE
       SRL  R7,8
       JNE  COMPTC            THIS IS THE RECORD LENGTH
       MOV  @XRECLN(R9),R7    USE EXTENDED
COMPTC MOV  @LRECLN,R11       TEST IF PAB REC LENGTH IS ZERO
       JNE  TRLN              BRANCH IF NOT ZERO
       MOV  R7,@LRECLN        IF ZERO PUT IN CORRECT LENGTH
TRLN   C    R7,@LRECLN        TEST IF LENGTHS ARE EQUAL
       JNE  BAD012            ERROR RECORD LENGTHS UNEQUAL
*
* set EOF offset parameters...
*
       MOV  @LEV3RC(R9),R0    GET MID AND LSB IF VARIABLE USED
       SWPB R0                    HIGHEST RECORD IF FIXED USED
       MOV  R0,@FNVNAU(R3)
*
       MOVB @EOFOST(R9),@FNVROF(R3)
       CLR  R4
       MOVB @NIBLES(R9),R4
       ANDI R4,EFOSMS
       MOVB R4,@FNVSOF(R3)
*
       CZC  @H0002,R6         test for open input
       JNE  OAP010            not input --- either output or append
       COC  @H0004,R6         test if open input
       JEQ  RLENPC
*
OAP010 COC  @H0010,R6         test fixed/variable records
*
* MODSET to correct problem with relative file access    Clint Pulley 92/11/22
* If opening in update mode, get sector and date/time data
*
       JEQ  OAP003            IF VARIABLE
*
* FIXED, MUST BE UPDATE MODE SO COPY REQUIRED DATA TO FCB
*
       MOV  @LEV2RC(R9),@HSALML(R3)
       MOVB @NIBLES(R9),R4
       SRL  R4,4
       MOVB R4,@HSALMS(R3)
       MOV  @DATLUF(R9),@LSTUP2(R3)
       MOV  @DATLUF+2(R9),@LSTUP1(R3)
       JMP  OAP002
*
* End of MODSET
*
* NOW TEST APPEND MODE
OAP003 COC  @H0006,R6         TEST IF APPEND MODE
       JNE  OAP002            NO
*
       MOV  @FNVNAU(R3),R5    IT IS APPEND SO MOVE # USED TO POINTER
       MOV  R5,@FNLOF2(R3)    GET MID AND LSB
       MOVB @EOFOST(R9),@FNLROF(R3) GET BYTE POINTER IN SECTOR
       MOVB R4,@FNLOFM(R3)    GET THE MSB OF THE SECTOR
       SRL  R4,8
       JNE  OAP000
       MOV  R5,R5
       JEQ  OAP002
OAP000 DEC  R5
       JOC  OAP001
       DEC  R4
OAP001 BLWP @GETAUS
       MOV  R0,R0
       JNE  BAD012
       MOV  R8,@RAMBUF
       BLWP @RSECTR           GET THE SECTOR TO APPEND TO
       JEQ  BAD012
       BL   @PUTDSL
OAP002 B    @RLEN1
       PAGE
*    DELETE FILE COMMAND
*
*
*
DELETE CI   R2,WBUFEN
       JNE  ISOPEN            BRANCH IF OPEN
       B    @ISNOPN
*
* TO BE HERE, TRYING TO DELETE A FILE ALREADY OPEN
* THEREFORE, GET TO TOP FCB
* R2 POINTS TO FILE NAME POINTER BLOCK
* GET FCB DATA BUFFER POINTER TO R3
*
ISOPEN
*
* TEST IF PROTECTED
*
TSTPRO MOVB @FSTATF(R9),R1
       COC  @H0800,R1
       JNE  DELOOP            BRANCH IF TRYING TO DELETE PROTECTED FILE
DELEMD B    @BADOP1
*
* NOW START DELETING AT THE TOP FCB
DELOOP BL   @TOPFCB
       JEQ  BAD012
*
       C    @AUNUM,@EMDKCB+2  yalfu
       JEQ  DELEMD            can't delete an active emulate file...
*
* TO BE HERE, AT THE TOP MOST FCB SO START DELETING
*
ATTOP  MOV  @FNLOCF(R3),@STRTSR  SAVE PTR TO FIRST FCB ON DISK
ATTOP1
* THIS ROUTINE DELETES OR FREES ALL SECTORS IN THE VIB
       MOV  R9,R6             GET PTR IN R6
       AI   R6,SEPTRS         TO DATA POINTERS
       MOV  R9,R5
       AI   R5,256            R5 IS ENDING CRITERION
       MOV  @AUTSFD(R9),R11   ANY TO RELEASE?
       JEQ  FRESE1            NO
* NOW USE TEST FOR END OF PTRS
FRESE2 C    R6,R5
       JEQ  FRESE1            HAVE GONE TROUGH SECTOR
       MOV  *R6+,R1
       JEQ  FRESE1            A NON EXISTANT POINTER
       MOV  *R6+,R2           OTHERWISE CONTINUE
       S    R1,R2             NOW R9 CONTAINS THE NUMBER TO DELETE
       INC  R2
       S    R2,R11
       JOC  FRESE3
       A    R2,R11
       JEQ  FRESE1
       MOV  R11,R2
* NOW GET DISK MAP FROM TRACK 0
FRESE3 DEC  R2
       BLWP @FRBITM           ZERO OUT BITS
       MOV  R0,R0             CHECK FOR ERROR
       JEQ  FRESE2            NO ERROR
FRSER0 B    @BADOPC           CLEAR OUT THE PAB IN USE AND ERROR
*
* NOW FREE UP THIS SECTOR ITSELF
* R3 POINTS TO FILE NAME BUFFER SO GET SECTOR THERE
*
FRESE1 MOV  @FNLOCF(R3),R1
       CLR  R2
       BLWP @FRBITM
       MOV  R0,R0
       JNE  FRSER0
* NOW GET NEXT IN STRING
* SIBLIN POINTS TO FCB SO SEE IF A NEXT FCB IN CHAIN
       MOV  @PTRNXT(R9),@FNLOCF(R3)
       JEQ  DELNAM            JUMP IF COMPLETE
* OTHERWISE BRING IN NEXT FCB
       MOV  @NIBLES(R9),R1    GET AU NUMBER
       ANDI R1,>000F
H000F  EQU  $-2
       MOV  R1,@SCINAU        FIRST SECTOR
       MOV  R9,@RAMBUF
       MOV  @FNLOCF(R3),@AUNUM
*      SRL  R1,12
*      MOVB R1,@FNSEC0(R3)
       BLWP @RSECTR
       JEQ  FRSER0            READ ERROR
       JMP  ATTOP1            CONTINUE UNTIL ALL SECTORS FREED
*
* TO BE HERE, IT COULD BE DELETE, OR SAVE, OR WRITE DIRECT, OR OPEN OUTPUT
*
DELNAM SZCB @FNSEC0(R9),@FNSEC0(R9)  ZERO OUT POINTERS TO SIBLINS
       MOV  @OPCODE,R6    GET OPCODE
       CB   R6,@CBH07           TEST IF DELETE
       JEQ  DELNA1            BRANCH IF DELETE
* OTHERWISE GET TOP FCB BACK FROM BIT MAP
       MOV  @STRTSR,R0
       MOV  @PARFPT(R9),@STRTSR SAVE PTR TO DIR OF FILE PTRS
       MOV  R8,R1              BUFFER USED BY GETSEC
COT001 LI   R5,1
       BLWP @GETSEC
       MOV  R0,R0             ERROR IN DEVICE?
       JNE  FRSER0
* NOW JUST CONTINUE
COT000 B    @CONTIN
*
* NOW MUST DELETE THE NAME FROM THE DIRECTORY AND FREE THE TWO OR THREE DATA
* BUFFERS USED.
* PTR TO DIRECTORY IS IN THE FILE BLOCK DESCRIPTOR. R3 POINTS TO FCB
* TO FCB
*
DELNA1 MOV  @INDPTR(R3),@AUNUM  GET POINTER
       MOV  R8,@RAMBUF        GET BUFFER
       CLR  @SCINAU           FIRST SECTOR IN AU
       BLWP @RSECTR           GET PTR TO FILES
       JEQ  FRSER0
*
* NOW GET THE PTR FROM NOPTRS WHERE IT IS IN STACK, DELETE IT AND MOVE UP THE
* REST.
*
       MOV  @STRTSR,R5        R5 HAS VALUE TO LOOK FOR
       MOV  R8,R7             R7 PTS TO TOP
       CLR  R6                R6 IS COUNTER
DEL1   C    *R7+,R5
       JEQ  MAT               BRANCH IF A MATCH
       INCT R6                OTHERWISE INCR6
       JMP  DEL1
*
MAT    CI   R6,252            NOW TEST IF COMPLETE
       JEQ  DEL2
HFFFE  EQU  $+2
CBHFE  EQU  $+3
       MOV  *R7,@-2(R7)
       INCT R7
       INCT R6
       JMP  MAT
*
DEL2   CLR  @-2(R7)
* NOW SUBTRACT 1 FROM NUMBER OF FILE PTRS BUT FIRST WRITE THIS
       BLWP @WSECTR
       JEQ  FRSER0            ERROR?
DEL3   MOV  @254(R8),@AUNUM
       BLWP @RSECTR
       JEQ  FRSER0
* NOW TO SUBTRACT
       AB   @CBHFF,@NUMFIL(R8)
* WRITE IT BACK
       BLWP @WSECTR
       JMP  FRSER0            WILL REPORT ERROR IF NECESSARY
*
FRESEC DATA WRKREG,FRESEF
*
FRESEF MOV  @2(R13),R1
       MOV  R1,R0
       SRL  R1,3
       AI   R1,FDKVIB+BITMAP
       LI   R10,>0100
       MOVB R10,@VIBCHG
       ANDI R0,>0007
       JEQ  ZNBYTE
       SLA  R10,0
ZNBYTE MOVB *R1,R8
       SZCB R10,R8
       MOVB R8,*R1
       RTWP
*
PBFST  CLR  R5
       COC  @H0010,R6
       JNE  NVAR1
       SOCB @CBH80,R5
NVAR1  COC  @H0008,R6
       JNE  NINTE
       SOCB @CBH02,R5
NINTE  RT
*
GETCLD MOV  @PTRNXT(R9),R0      GET THE CHILD FCB
       JEQ  GETC00            NONE TO GET
       MOV  R0,@AUNUM
       MOV  @NIBLES(R9),R7
       ANDI R7,>000F
       MOV  R7,@SCINAU
       BLWP @RSECTR           EQ BIT SET IF ERROR IN READ, AND R0 SET
GETC00 RT                     CONTINUE
*
PBTOCB MOV  @BUFADR,@PASADL   MOVE THE NEW NAME POINTED TO
       MOVB @BUFADH,@PASADH   BY THE ADDRESS BUFFER POINTER
       SETO @PASRW            USER TO DSR
       MOV  @H000A,@PASCNT    ONLY 10 CHARACTERS
       MOVB @CPUFLG,@PASVDP
       LI   R0,COMBUF         TO COMBUF
       MOV  R0,@PASDSR
       BLWP @PASDAT           MOVE IT
       RT
